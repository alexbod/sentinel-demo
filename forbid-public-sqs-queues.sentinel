# Validate if all SQS queues have KMS enabled

##### Imports #####

# ***** Imports ***** #
# insert imports here
import "tfplan"
import "strings"
import "json"

# ******************

# ***** Shared Functions ***** #
# Shared functions to help writing policies.
find_resources = func(type) {
	resources = {}
	# Iterate over all modules in the tfplan import
	for tfplan.module_paths as path {
		# Iterate over the named resources of desired type in the module
		for tfplan.module(path).resources[type] else {} as name, instances {
			# Iterate over resource instances
			for instances as index, r {

				# Get the address of the instance
				if length(path) == 0 {
					# root module
					address = type + "." + name + "[" + string(index) + "]"
				} else {
					# non-root module
					address = "module." + strings.join(path, ".module.") + "." +
						type +
						"." +
						name +
						"[" +
						string(index) +
						"]"
				}

				# Add the instance to resources map, setting the key to the address
				resources[address] = r
			}
		}
	}

	return resources
}


##### Functions #####

# Function to validate that encryption is enabled
validate_encryption = func() {

	# Initialize booleans to true
	# They will be set to false if any instances violate rules
	result = {
		"encrypted": true,
	}

	# Get all resources of specified type
	resource_instances = find_resources("aws_sqs_queue")

	# Loop through the resource instances
	for resource_instances as address, r {

		# Check whether server-side encryption is enabled
		if length(r.applied.kms_master_key_id) else 0 == 0 {
			print("SQS", address, "is not encrypted.")
			print("Please enable server side encryption. Example:")
			print("kms_master_key_id                 = \"alias/aws/sqs\"")
			result["encrypted"] = false
		}

	} // end resource instances

	# Return result with booleans which will be false if there were any violations
	return result
}

##### Rules #####

# Call the validation function
validations = validate_encryption()

# Main rule
main = rule {
	validations["encrypted"]
}

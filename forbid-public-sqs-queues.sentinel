# Validate if all SQS queues are not public

##### Imports #####

import "tfplanfunctions"
import "json"

# Check if Principal == "*" for a SQS policy
check_policy = func(policy) {
	if policy == "" {
		return true # empty policy
	}
	j = json.unmarshal(policy)
	for j["Statement"] as statement {
		if statement["Principal"] == "*" {
			return false
		}
	}
	return true
}

##### Functions #####

# Function to validate that all SQS queues are not public
public_sqs = func() {

	# Get all resources of specified type
	resource_instances = tfplanfunctions.find_resources_from_plan("aws_sqs_queue_policy")

	# Loop through the resource instances
	for resource_instances as address, r {
	
		# Check for SQS policy value
		if (check_policy(r.diff.policy.new) == false) {
			return false
		}

	} // end resource instances

	return true
}

##### Rules #####

# Main rule
main = rule {
	public_sqs()
}
